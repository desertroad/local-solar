plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'org.jetbrains.dokka' version '1.9.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.desertroad'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(8)
}

tasks.register("javadocJar", Jar) {
    archiveClassifier = "javadoc"
    from(tasks.dokkaJavadoc)
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Local Solar"
                description = "Calculate the position of the sun at a specific moment and the times of events such as sunrise and sunset."
                url = "https://github.com/desertroad/local-solar"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/license/mit/"
                    }
                }

                developers {
                    developer {
                        id = "lulurala"
                        name = "Kim Heeyong"
                        email = "wildcaravan@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/desertroad/local-solar.git"
                    developerConnection = "scm:git:ssh://git@github.com/desertroad/local-solar.git"
                    url = "https://github.com/desertroad/local-solar"
                }
            }
        }
    }

    repositories {
        maven {
            name = "sonatype"
            def releasesRepoUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            credentials(PasswordCredentials)
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}